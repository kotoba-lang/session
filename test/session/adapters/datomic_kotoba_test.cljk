(ns session.adapters.datomic-kotoba-test
  (:require [clojure.test :refer [deftest is]]
            [session.adapters.datomic-kotoba :as dk]
            [session.core :as c]
            [session.model :as m]
            [session.ports :as p]))

(deftest stores-sessions-through-datomic-kotoba-adapter
  (let [state (atom {:sessions {} :events {} :txs []})
        conn (dk/memory-datomic-kotoba state)
        store (dk/datomic-kotoba-session-store conn {:tenant "kotoba"})
        session (m/session "s-datomic-1" "did:web:example.com:alice"
                           {:created-at "2026-07-01T00:00:00Z"})]
    (c/create! store session)
    (is (= session (p/get-session store "s-datomic-1")))
    (is (= ["s-datomic-1"]
           (mapv :session/id
                 (p/sessions-for-subject store "did:web:example.com:alice"))))
    (c/revoke! store "s-datomic-1" :logout "2026-07-01T00:01:00Z")
    (is (= [:revoked]
           (mapv :session.event/type (p/events-for store "s-datomic-1"))))))

(deftest emits-datomic-style-lookup-refs-and-attrs
  (let [state (atom {:sessions {} :events {} :txs []})
        conn (dk/memory-datomic-kotoba state)
        store (dk/datomic-kotoba-session-store conn {})
        session (m/session "s-datomic-2" "did:web:example.com:alice" {})]
    (c/create! store session)
    (c/revoke! store "s-datomic-2" :logout "2026-07-01T00:01:00Z")
    (is (= [[:session/id "s-datomic-2"]
            [:session/id "s-datomic-2"]
            [:session.event/id "s-datomic-2:revoked:2026-07-01T00:01:00Z"]]
           (mapv (comp :db/id first :tx-data) (:txs @state))))
    (is (= {:session/id "s-datomic-2"
            :session/subject "did:web:example.com:alice"
            :session/status :active}
           (select-keys (-> @state :txs first :tx-data first)
                        [:session/id :session/subject :session/status])))))
